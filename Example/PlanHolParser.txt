\hshead {
    module ALPHA2.PlanHolParser where

    import ALPHA2.Header
    import ALPHA2.PlanHolLexer
}

\target {
    token-type: "Token"
    parser-name: "runHolParser"
    result-type: "Either TermRep [DeclRep]"
    start: $Either $Query ($Sequence $Decl)
    terminals:
        "T_dot loc": $dot 0 none
        "T_arrow loc": $arrow 95 right
        "T_lparen loc": $lparen 0 none
        "T_rparen loc": $rparen 0 none
        "T_lbracket loc": $lbracket 0 none
        "T_rbracket loc": $rbracket 0 none
        "T_quest loc": $quest 0 none
        "T_pi loc": $pi 0 none
        "T_sigma loc": $sigma 0 none
        "T_succ loc": $succ 0 none
        "T_bslash loc": $bslash 80 right
        "T_if loc": $if 79 none
        "T_semicolon loc": $semicolon 78 left
        "T_fatarrow loc": $fatarrow 77 right
        "T_comma loc": $comma 76 left
        "T_eq loc": $eq 75 none
        "T_le loc": $le 75 none
        "T_lt loc": $lt 75 none
        "T_ge loc": $ge 75 none
        "T_gt loc": $gt 75 none
        "T_is loc": $is 75 none
        "T_cons loc": $cons 74 right
        "T_plus loc": $plus 73 left
        "T_minus loc": $minus 73 left
        "T_star loc": $star 72 left
        "T_slash loc": $slash 72 left
        "T_cut loc": $cut 0 none
        "T_true loc": $true 0 none
        "T_fail loc": $fail 0 none
        "T_debug loc": $debug 0 none
        "T_kind loc": $kind 0 none
        "T_type loc": $type 0 none
        "T_id loc contents": $id 0 none
        "T_nat_lit loc contents": $natlit 0 none
        "T_chr_lit loc contents": $chrlit 0 none
        "T_str_lit loc contents": $strlit 0 none
}

\define $Query : "TermRep" {
    95 [$quest, $TermRep0, $dot]:
        $2
}

\define $Decl : "DeclRep" {
    95 [$kind, $id, $KindRep0, $dot]:
        RKindDecl ($1.loc <> $4.loc) (TC_Named $2.contents) $3
    95 [$type, $id, $TypeRep0, $dot]:
        RTypeDecl ($1.loc <> $4.loc) (DC_Named $2.contents) $3
    95 [$TermRep0, $dot]:
        RFactDecl (getSLoc $1 <> $2.loc) $1
}

\define $KindRep0 : "KindRep" {
    80 [$KindRep1, $arrow, $KindRep0]:
        RKArr (getSLoc $1 <> getSLoc $3) $1 $3
    80 [$KindRep1]:
        $1
}

\define $KindRep1 : "KindRep" {
    75 [$type]:
        RStar $1.loc
    75 [$lparen, $KindRep0, $rparen]:
        RKPrn ($1.loc <> $3.loc) $2
}

\define $TypeRep0 : "TypeRep" {
    80 [$TypeRep1, $arrow, $TypeRep0]:
        RTyApp (getSLoc $1 <> getSLoc $3) (RTyApp (getSLoc $1 <> $2.loc) (RTyCon $2.loc TC_Arrow) $1) $3
    80 [$TypeRep1]:
        $1
}

\define $TypeRep1 : "TypeRep" {
    75 [$TypeRep1, $TypeRep2]:
        RTyApp (getSLoc $1 <> getSLoc $2) $1 $2
    75 [$TypeRep2]:
        $1
}

\define $TypeRep2 : "TypeRep" {
    70 [$id]:
        if head $1.contents `elem` ['A' .. 'Z'] then RTyVar $1.loc $1.contents else RTyCon $1.loc (TC_Named $1.contents)
    70 [$lparen, $TypeRep0, $rparen]:
        RTyPrn ($1.loc <> $3.loc) $2
}

\define $TermRep0 : "TermRep" {
    80 [$id, $bslash, $TermRep0]:
        RAbs ($1.loc <> getSLoc $3) $1.contents $3
    80 [$TermRep1L0]:
        $1
}

\define $TermRep1L0 : "TermRep" {
    70 [$TermRep1L1, $if, $TermRep1L1]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc (DC_LO LO_if)) $1) $3
    70 [$TermRep1L1]:
        $1
}

\define $TermRep1L1 : "TermRep" {
    70 [$TermRep1L1, $semicolon, $TermRep1L2]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc (DC_LO LO_or)) $1) $3
    70 [$TermRep1L2]:
        $1
}

\define $TermRep1L2 : "TermRep" {
    70 [$TermRep1L3, $fatarrow, $TermRep1L2]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc (DC_LO LO_imply)) $1) $3
    70 [$TermRep1L3]:
        $1
}

\define $TermRep1L3 : "TermRep" {
    70 [$TermRep1L3, $comma, $TermRep1L4]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc (DC_LO LO_and)) $1) $3
    70 [$TermRep1L4]:
        $1
}

\define $TermRep1L4 : "TermRep" {
    70 [$TermRep1L5, $is, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc (DC_LO LO_is)) $1) $3
    70 [$TermRep1L5, $eq, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_eq) $1) $3
    70 [$TermRep1L5, $le, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_le) $1) $3
    70 [$TermRep1L5, $lt, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_lt) $1) $3
    70 [$TermRep1L5, $ge, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_ge) $1) $3
    70 [$TermRep1L5, $gt, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_gt) $1) $3
    70 [$TermRep1L5]:
        $1
}

\define $TermRep1L5 : "TermRep" {
    70 [$TermRep1L6, $cons, $TermRep1L5]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_Cons) $1) $3
    70 [$TermRep1L6]:
        $1
}

\define $TermRep1L6 : "TermRep" {
    70 [$TermRep1L6, $plus, $TermRep1L7]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_plus) $1) $3
    70 [$TermRep1L6, $minus, $TermRep1L7]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_minus) $1) $3
    70 [$TermRep1L7]:
        $1
}

\define $TermRep1L7 : "TermRep" {
    70 [$TermRep1L7, $star, $TermRep1L8]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_mul) $1) $3
    70 [$TermRep1L7, $slash, $TermRep1L8]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_div) $1) $3
    70 [$TermRep1L8]:
        $1
}

\define $TermRep1L8 : "TermRep" {
    70 [$TermRep1L9]:
        $1
}

\define $TermRep1L9 : "TermRep" {
    70 [$TermRep2]:
        $1
}

\define $TermRep2 : "TermRep" {
    60 [$TermRep3, $id, $bslash, $TermRep0]:
        RApp (getSLoc $1 <> getSLoc $4) $1 (RAbs ($2.loc <> getSLoc $4) $2.contents $4)
    60 [$TermRep3]:
        $1
}

\define $TermRep3 : "TermRep" {
    10 [$TermRep3, $TermRep4]:
        RApp (getSLoc $1 <> getSLoc $2) $1 $2
    10 [$TermRep4]:
        $1
    10 [$pi]:
        RCon $1.loc (DC_LO LO_pi)
    10 [$sigma]:
        RCon $1.loc (DC_LO LO_sigma)
    10 [$succ]:
        RCon $1.loc DC_Succ
}

\define $TermRep4 : "TermRep" {
    5 [$lparen, $TermRep0, $rparen]:
        RPrn ($1.loc <> $3.loc) $2
    5 [$cut]:
        RCon $1.loc (DC_LO LO_cut)
    5 [$true]:
        RCon $1.loc (DC_LO LO_true)
    5 [$fail]:
        RCon $1.loc (DC_LO LO_fail)
    5 [$debug]:
        RCon $1.loc (DC_LO LO_debug)
    5 [$id]:
        if head $1.contents `elem` ['A' .. 'Z'] then RVar $1.loc $1.contents else RCon $1.loc (DC_Named $1.contents)
    5 [$natlit]:
        mkNatLit $1.loc $1.contents
    5 [$strlit]:
        mkStrLit $1.loc $1.contents
    5 [$chrlit]:
        mkChrLit $1.loc $1.contents
    5 [$lbracket, $rbracket]:
        RCon ($1.loc <> $2.loc) DC_Nil
    5 [$lbracket, $ListBody, $rbracket]:
        RPrn ($1.loc <> $3.loc) $2
}

\define $ListBody : "TermRep" {
    10 [$TermRep1L5]:
        RApp (getSLoc $1) (RApp (getSLoc $1) (RCon (getSLoc $1) DC_Cons) $1) (RCon (getSLoc $1) DC_Nil)
    10 [$TermRep1L5, $comma, $ListBody]:
        RApp (getSLoc $1 <> getSLoc $3) (RApp (getSLoc $1 <> $2.loc) (RCon $2.loc DC_Cons) $1) $3
}

\define $Sequence ($Elem : "a") : "[a]" {
    10 []:
        []
    10 [$Elem, $Sequence $Elem]:
        $1 : $2
}

\define $Either ($Left : "a") ($Right : "b") : "Either a b" {
    10 [$Left]:
        Left $1
    10 [$Right]:
        Right $1
}

\hstail {
}
