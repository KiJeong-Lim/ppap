\hshead {
    module BlockArgument where
}

\target {
    token-type: "Tok"
    parser-name: "parser"
    result-type: "Term"
    start: $Term0
    terminals:
        "LargeId nm": $lid 0 none
        "SmallId nm": $sid 0 none
        "LParen": $lprn 0 none
        "RParen": $rprn 0 none
        "Lambda": $lambda 0 none
        "InfixSym nm": $infixsym 85 none
}

\define $Term0 : "Term" {
    90 [$lid, $lambda, $Term0]:
        Lam $1.nm $3
    90 [$sid, $lambda, $Term0]:
        Lam $1.nm $3
    90 [$Term1]:
        $1
}

\define $Term1 : "Term" {
    80 [$Term2, $infixsym, $Term2]:
        App (App (Var $2.nm) $1) $3
    80 [$Term2]:
        $1
}

\define $Term2 : "Term" {
    70 [$Term3, $lid, $lambda, $Term0]:
        App $1 (Lam $2.nm $4)
    70 [$Term3, $sid, $lambda, $Term0]:
        App $1 (Lam $2.nm $4)
    70 [$Term3]:
        $1
}

\define $Term3 : "Term" {
    60 [$Term3, $Term4]:
        App $1 $2
    60 [$Term4]:
        $1
}

\define $Term4 : "Term" {
    50 [$lid]:
        Var $1.nm
    50 [$sid]:
        Var $1.nm
    50 [$lprn, $Term0, $rprn]:
        $2
}

\hstail {
    data Tok
        = LargeId String
        | SmallId String
        | LParen
        | RParen
        | Lambda
        | InfixSym String
        deriving (Eq, Ord, Show)

    data Term
        = Lam String Term
        | App Term Term
        | Var String
        deriving (Eq, Ord, Show)

    main :: IO ()
    main = do
        let example = [SmallId "sigma", LargeId "X", Lambda, LargeId "X", InfixSym "=", LargeId "X"]
        print (parser example)
}
